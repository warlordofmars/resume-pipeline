/*
 * resume/build.gradle
 */

import java.util.Random
import groovy.xml.XmlUtil
import groovy.xml.StreamingMarkupBuilder

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.30"
        classpath "javax.xml.bind:jaxb-api:2.3.0"
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
}

project.version = scmVersion.version

ext {
    cloudformationBucket = 'warlordofmars-cloudformation'
    testWebsiteUrl = 'resume-test.warlordofmars.net'
    prodWebsiteUrl = 'resume.warlordofmars.net'
    testStackName = "ResumeWebsiteTest"
    prodStackName = 'ResumeWebsite'
    cloudformationSource = 'website.yaml'
    domainName = 'warlordofmars.net'
    wildcardCertARN = "arn:aws:acm:us-east-1:247631471946:certificate/471106fc-e3dd-4e0b-a20f-010a6e326283"
    productionBranch = 'master'
    ensureStrings = ['John A. Carter', 'johncarter@warlordofmars.net', '(770) 598-7096']
    resumeSource = 'resume.json'
    htmlResumeTheme = 'theme/node_modules/jsonresume-theme-class/'
    pdfResumeTheme = 'theme/node_modules/jsonresume-theme-short/'
    spellCheckIgnoreList = 'spell_check_ignore.txt'
    resumeFormats = ['html', 'pdf', 'yml', 'md']
    productionNumberOfCopies = 10
}

if (!project.hasProperty('previewHtmlFileName')) {
    def random = new Random()
    def randomNumber = random.nextInt() & Integer.MAX_VALUE
    ext.previewHtmlFileName = "resume-preview-${randomNumber}.html"
}

if (!project.hasProperty('productionHtmlFileName')) {
    ext.productionHtmlFileName = 'index.html'
}

 def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

def isProd() {
    return (gitBranch() == productionBranch)
}

def getWebsiteUrl() {
    return ( isProd() ? prodWebsiteUrl : testWebsiteUrl)
}

def getStackName() {
    return ( isProd() ? prodStackName : testStackName)
}

def getHtmlResumeName() {
    return ( isProd() ? productionHtmlFileName : previewHtmlFileName)
}

def getNumberOfCopies() {
    return ( isProd() ? productionNumberOfCopies : 1 )
}


ext.stackName = getStackName()
ext.websiteUrl = getWebsiteUrl()

task('clean', type:Delete) {
    delete buildDir
}

task('checkPrerequisites') {
    doLast {
        def prerequisites = [
            'hackmyresume': 'Install via \'npm install -g hackmyresume\'',
            'aspell': 'Install via \'brew install aspell\'',
            'lp': 'This is a built-in that should already be there.  Why isn\'t it there?',
            'aws': 'Install via \'brew install awscli\'',
            'wkhtmltopdf': 'Install via \'brew install wkhtmltopdf\'',
            'cfn-lint': 'Install via \'pip install cfn-lint\'',
            'cfn_nag_scan': 'Install via \'gem install cfn-nag\''
        ]
        prerequisites.each { prerequisite, installNotes ->
            def checkPrereq = exec {
                commandLine 'which', prerequisite
                ignoreExitValue true
                standardOutput new ByteArrayOutputStream()
            }
            if (checkPrereq.getExitValue() != 0) {
                throw new GradleException("It appears that prerequisite '${prerequisite}' is not installed.\n\n${installNotes}\n")
            }

        }
    }
}

task('processTestResults') {
    doLast {
        def reportFile = file("${buildDir}/report.xml")
        def xml = new XmlSlurper().parseText(reportFile.text)

        xml.testsuite.each { testsuite ->
            testsuite.testcase.each { testcase ->
                subprojects.each { p ->
                    if(p.hasProperty('tests')) {
                        p.tests.each { name, test ->
                            def testObj = project.ext."${name}"
                            if(!testObj.skipped) {
                                if("${testObj.classname}" == "${testcase.@classname}" && "${testObj.name}" == "${testcase.@name}") {
                                    println "Found test result: ${testcase.@name}"
                                    testcase.skipped.replaceNode { }
                                    if(testObj.output) {
                                        testcase.appendNode {
                                            'system-out'(testObj.output)
                                        }
                                    }
                                    if(!testObj.success) {
                                        testcase.appendNode {
                                            failure(type: testObj.failureType, details: testObj.output)
                                        }
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }

        reportFile.withWriter { outWriter ->
            XmlUtil.serialize(xml, outWriter)
        }

        println "\nSaved generated test report file to: ${reportFile}"
        
    }
}



class Test {

    String name
    String classname
    Boolean skipped
    Boolean success
    String output
    String failureType
    
    Test(classname, name) {
        this.classname = classname
        this.name = name
        this.skipped = true
    }

    void success(String output) {
        this.skipped = false
        this.success = true
        this.output = output
        println output
    }

    void failure(String type, String output) {
        this.skipped = false
        this.success = false
        this.output = output
        this.failureType = type
        throw new GradleException("${this.failureType}:\n${this.output}")
    }
}

task('registerTests') {
    doFirst {
        
        def sw = new StringWriter()
        def xml = new groovy.xml.MarkupBuilder(sw)
        xml.testsuites {
            subprojects.each { p ->
                if(p.hasProperty('tests')) {
                    testsuite(tests: p.tests.size()) {
                        p.tests.each { name, test ->
                            project.ext."${name}" = new Test(test[0], test[1])
                            testcase(classname: test[0], name: test[1]) {
                                skipped()
                            }
                        }
                    }
                }
            }
        }

        def reportFile = file("${buildDir}/report.xml")
        if(!reportFile.exists()) {
            file("${buildDir}").mkdirs()
            reportFile.write(sw.toString())
        }
    }
    
}