import groovy.json.JsonSlurper

apply plugin: 'jp.classmethod.aws.cloudformation'


ext.tests = [
    cfnTemplateDeployedTest: [cloudformationSource, 'Resume Website CloudFormation Template Deployed'],
    cfnLintTest: [cloudformationSource, 'Resume Website CloudFormation Linting / Syntax Check'],
    cfnNagTest: [cloudformationSource, 'Resume Website CloudFormation Best Practices'],
]

cloudFormation {
    stackName "${rootProject.ext.stackName}"
    templateFile file(cloudformationSource)
    templateBucket cloudformationBucket
    templateKeyPrefix stackName
    stackParams ([
        'DomainName': domainName,
        'FullDomainName': websiteUrl,
        'AcmCertificateArn': wildcardCertARN
    ])
}


task('deploy') {
    dependsOn awsCfnMigrateStackAndWaitCompleted, rootProject.registerTests
    finalizedBy rootProject.processTestResults
    doLast {

        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'aws', 'cloudformation', 'describe-stacks', '--stack-name', getStackName()
            standardOutput out
        }
        
        def stackDetails = new JsonSlurper().parseText(out.toString())['Stacks'][0]
        

        def message = "cloudformation stack \"${stackDetails['StackName']}\" has status: \"${stackDetails['StackStatus']}\"\n\nstack created: \"${stackDetails['CreationTime']}\"\nstack last updated: \"${stackDetails['LastUpdatedTime']}\"\n\n"
        stackDetails['Outputs'].each { output ->
            message = message + "${output['OutputKey']}: \"${output['OutputValue']}\"\n"
        }

        if(stackDetails['StackStatus'] in ["CREATE_COMPLETE", "UPDATE_COMPLETE"]) {
            cfnTemplateDeployedTest.success(message)
            println message
        } else {
            cfnTemplateDeployedTest.failure('CloudFormation Failed to Deploy', message)
            throw new GradleException(message)
        }
        
    }
}

awsCfnUploadTemplate.finalizedBy deploy
awsCfnMigrateStack.finalizedBy deploy
awsCfnWaitStackComplete.finalizedBy deploy
awsCfnMigrateStackAndWaitCompleted.finalizedBy deploy

task('delete') {
    dependsOn awsCfnDeleteStackAndWaitCompleted
}

awsCfnMigrateStack.dependsOn awsCfnUploadTemplate

task('cfnLint') {
    dependsOn rootProject.checkPrerequisites, rootProject.registerTests
    finalizedBy rootProject.processTestResults
    doFirst {
        def out = new ByteArrayOutputStream()
        def result = exec {
            commandLine 'cfn-lint', cloudformationSource
            standardOutput out
            ignoreExitValue true
        }
        if(result.getExitValue() == 0) {
            cfnLintTest.success('All Checks Passed')
        } else {
            cfnLintTest.failure('CloudFormation Syntax Error', out.toString())
            throw new GradleException(out.toString())
        }
    }
}

task('cfnNag') {
    dependsOn rootProject.checkPrerequisites, rootProject.registerTests
    finalizedBy rootProject.processTestResults
    doFirst {
        def out = new ByteArrayOutputStream()
        def result = exec {
            commandLine 'cfn_nag_scan', '--input-path', cloudformationSource
            standardOutput out
            ignoreExitValue true
        }
        if(result.getExitValue() == 0) {
            cfnNagTest.success(out.toString())
            println out.toString()
        } else {
            cfnNagTest.failure('Not Adhering to CloudFormation Best Practices', out.toString())
            throw new GradleException(out.toString())
        }
        
    }
}

task('build') {
    dependsOn cfnLint, cfnNag
}

