import groovy.json.JsonSlurper

apply plugin: 'jp.classmethod.aws.cloudformation'
    
cloudFormation {
    stackName "${rootProject.ext.stackName}"
    templateFile file(cloudformationSource)
    templateBucket cloudformationBucket
    templateKeyPrefix stackName
    stackParams ([
        'DomainName': domainName,
        'FullDomainName': websiteUrl,
        'AcmCertificateArn': wildcardCertARN
    ])
}

task('deploy') {
    dependsOn awsCfnMigrateStackAndWaitCompleted
    finalizedBy rootProject.processTestResults
    doLast {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'aws', 'cloudformation', 'describe-stacks', '--stack-name', getStackName()
            standardOutput out
        }
        
        def stackDetails = new JsonSlurper().parseText(out.toString())['Stacks'][0]
        def message = "${stackDetails['StackName']} status: ${stackDetails['StackStatus']}"
        rootProject.ext.testResults << [classname: cloudformationSource, name: 'Resume Website CloudFormation Template Deployed', systemOut: message]
    }
}

task('delete') {
    dependsOn awsCfnDeleteStackAndWaitCompleted
}

awsCfnMigrateStack.dependsOn awsCfnUploadTemplate

task('cfnLint') {
    dependsOn rootProject.checkPrerequisites
    finalizedBy rootProject.processTestResults
    doFirst {
        exec {
            commandLine 'cfn-lint', cloudformationSource
        }
        rootProject.ext.testResults << [classname: cloudformationSource, name: 'Resume Website CloudFormation Linting / Syntax Check']
    }
}

task('cfnNag') {
    dependsOn rootProject.checkPrerequisites
    finalizedBy rootProject.processTestResults
    doFirst {
        def out = new ByteArrayOutputStream()
        exec {
            commandLine 'cfn_nag_scan', '--input-path', cloudformationSource
            standardOutput out
        }
        rootProject.ext.testResults << [classname: cloudformationSource, name: 'Resume Website CloudFormation Best Practices', systemOut: out.toString()]
        println out.toString()
    }
}

task('build') {
    dependsOn cfnLint, cfnNag
}